# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

declare_args() {
  is_debug = true

  # On Mac, produce dSYM files.
  enable_dsyms = !is_debug
}

if (target_os == "") {
  target_os = host_os
}
if (current_os == "") {
  current_os = target_os
}
if (target_cpu == "") {
  target_cpu = host_cpu
}
if (current_cpu == "") {
  current_cpu = target_cpu
}

if (current_os == "win") {
  is_linux = false
  is_mac = false
  is_posix = false
  is_win = true
} else if (current_os == "mac") {
  is_linux = false
  is_mac = true
  is_posix = true
  is_win = false
} else if (current_os == "linux") {
  is_linux = true
  is_mac = false
  is_posix = true
  is_win = false
}

# Holds all configs used for making native executables and libraries, to avoid
# duplication in each target below.
_native_compiler_configs = [
  "//build/config/compiler:compiler",
  "//build/config/compiler:default_include_dirs",
  "//build/config/compiler:default_warnings",
]

# Optimizations and debug checking.
if (is_debug) {
  _native_compiler_configs += [
    "//build/config:debug",
    "//build/config/compiler:no_optimize",
    "//build/config/compiler:symbols",
  ]
} else {
  _native_compiler_configs += [
    "//build/config:release",
    "//build/config/compiler:optimize",
    "//build/config/compiler:no_symbols",
  ]
}

set_defaults("executable") {
  configs = _native_compiler_configs
}

set_defaults("static_library") {
  configs = _native_compiler_configs
}

set_defaults("source_set") {
  configs = _native_compiler_configs
}

if (is_win) {
  set_default_toolchain("//build/toolchain/win")
} else if (is_mac) {
  host_toolchain = "//build/toolchain/mac:clang_$host_cpu"
  set_default_toolchain("//build/toolchain/mac:clang_x64")
}
